apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = '1.8'

mainClassName = 'jmodern.Main'
version = '0.1.0'

repositories {
    mavenCentral()
}

configurations {
    capsule
}

dependencies {
    compile 'io.dropwizard:dropwizard-client:0.7.0'
    compile 'io.dropwizard:dropwizard-core:0.7.0'
    compile 'io.dropwizard:dropwizard-db:0.7.0'
    compile 'io.dropwizard:dropwizard-jdbi:0.7.0'
    compile 'com.netflix.feign:feign-core:6.1.2'
    compile 'com.netflix.feign:feign-jaxrs:6.1.2'
    compile 'com.netflix.feign:feign-jackson:6.1.2'
    runtime 'com.h2database:h2:1.4.178'
    compile 'org.jooq:jooq:3.3.2'
    compile 'com.squareup.dagger:dagger:1.2.1'
    compile 'com.squareup.dagger:dagger-compiler:1.2.1'
    capsule 'co.paralleluniverse:capsule:0.4.0'
    testCompile 'junit:junit:4.11'
}

task capsule(type: Jar, dependsOn: classes) {
    archiveName = "jmodern-web.jar"

    from jar // embed our application jar
    from { configurations.runtime } // embed dependencies

    from(configurations.capsule.collect { zipTree(it) }) { include 'Capsule.class' } // we just need the single Capsule class

    from { sourceSets.main.resources }

    manifest {
        attributes(
            'Main-Class'  :   'Capsule',
            'Application-Class'   : mainClassName,
            'Application-Version' : version,
            'Min-Java-Version' : '1.8.0',
            'JVM-Args' : run.jvmArgs.join(' '),
            'System-Properties' : (run.systemProperties + ["dropwizard.config": '$CAPSULE_DIR/jmodern.yml']).collect { k,v -> "$k=$v" }.join(' '),
        )
    }
}

run {
    systemProperty "dropwizard.config", "build/resources/main/jmodern.yml"
}
